group 'com.hackhofer.uniqa.poc'
version '0.1'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.5.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'


repositories {
    mavenCentral()
}

task genJaxb {
    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
    ext.classesDir = "${buildDir}/classes/jaxb"
    ext.schema = "src/main/resources/calculation.xsd"

    outputs.dir classesDir

    doLast() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)
            mkdir(dir: classesDir)

            xjc(destdir: sourcesDir, schema: schema) {
                arg(value: "-wsdl")
                produces(dir: sourcesDir, includes: "**/*.java")
            }

            javac(destdir: classesDir, source: 1.6, target: 1.6, debug: true,
                  debugLevel: "lines,vars,source",
                  classpath: configurations.jaxb.asPath) {
                src(path: sourcesDir)
                include(name: "**/*.java")
                include(name: "*.java")
            }

            copy(todir: classesDir) {
                fileset(dir: sourcesDir, erroronmissingdir: false) {
                    exclude(name: "**/*.java")
                }
            }
        }
    }
}

configurations {
    jaxb
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")

    compile("joda-time:joda-time:2.7")
    compile("com.fasterxml.jackson.datatype:jackson-datatype-joda")

    compile("org.springframework.boot:spring-boot-starter-ws")
    compile("wsdl4j:wsdl4j:1.6.1")

    jaxb("com.sun.xml.bind:jaxb-xjc:2.2.4-1")

    compile(files(genJaxb.classesDir).builtBy(genJaxb))
    testCompile('org.springframework.boot:spring-boot-starter-test')

    compile("com.h2database:h2")

    compile("org.springframework.boot:spring-boot-starter-data-jpa")

    compile "io.springfox:springfox-swagger2:2.5.0"
    compile "io.springfox:springfox-swagger-ui:2.5.0"

}


jar {
    baseName = 'uniqua-poc-service'
    version =  '0.1.0'
    from genJaxb.classesDir
}

